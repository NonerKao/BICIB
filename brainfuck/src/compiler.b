/* compiler_b a self_hosted compiler for brainfuck
   Copyright 2015 Quey_Liang Kao

   This program is free software; you can redistribute it and/or modify it
   under the terms of the GNU General Public License as published by the
   Free Software Foundation; either version 2 or (at your option) any
   later version

   This program is distributed in the hope that it will be useful
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE  See the
   GNU General Public License for more details

   You should have received a copy of the GNU General Public License
   along with this program; if not write to the Free Software
   Foundation 51 Franklin Street Fifth Floor Boston MA 02110_1301 USA*/

>>[>]>
++++++++++++++++++++++++++++++++++++++++++++++>
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++<
.
>.
--------------.
--.
+++++++++++++++++.
-----------.
++++++.
-.
<--------------.
++++++++++++++.
>++++++.
---------------.
+++++++++++++++++++.
----.
>++++++++++.

<---------------------.
+++.
+++++++.
------.
++++++.
-------.
<++++++++++++.
>>.

<+++++++++++.
++.
+++++++.
<--------------------------.
+++++.
>----.
+.
---.
<+++++++.
-------.
>++.
----------------.
++++++++++++++++++++++.
>.

<-----.
++.
-------------------.
<-----.
++++.
++++++++++++.
>++++++++++++++++++++++.
<+.
+++++++.
------------.
-------.
>------.
----------------.
++++++++++++++++++++++.
>.

<-----------.
++.
+++++++.
<-----.
++++.
++++++++++++.
>++.
<+.
-----.
-------.
>------.
--------------.
++++++++++++++++++++.
>.
[-]<[-]<[-]

>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> at seq
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++> set to A
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++<
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

<< at the tail of code [<]
> at the head of code
[

[-<+>]
<[->>[>]>+<<[<]<]>
>[>]>

-[
  -[
    -[
      -[
        -[
          -[
            [-<+>]
            >----------------------------------------------------------------------------------------------------
             >-----------------------------------------------------------------------------------------------------
              >---------------------------------------------------------------------------------------------------
               >--------------------------------
                >------------------------------------
                 >------------------------------------------------------------------------------------------------------------------
                  >--------------------------------------------------------------------------------------------------
                   >------------------------------------------------------------------------------------------------------------------------
                    >----------
            <<<<<<<<<        
          ]for0x3e
          >-----
           >---------
          <<
        ]for0x3c
        >>>>------------------------------------------------------------------
            >+++++
             >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
              >----------------------
               >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                >------------------------------
                 >-------------------------------------
        <<<<<<<<<<
      ]for0x2b
      >+++++
       >+++++++++
      <<
    ]for0x2d
    >---------
     >----------
      >-------------------
       >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        >-----
         >------------------------------------------------------------------
          >++++++++++++++++++++++
           >------------------------------------------------------------------------
            >----
             >>>-----------------
                >+++++++++++++++
                 >+++++++++++++++++++++++++++++++
                  >---------------------------------------------------------------------------------------------------
                   >---------------------------------------------------------------------------------------------------------------
    <<<<<<<<<<<<<<<<
  ]for0x2c
  >>>>>>>>>>>>>>>>>>>>>>-
                        >>>>>>>>>>>>>>-
  <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
]for0x2e
>+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
 >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
   >++++++++++++++++++++++++++++++++
    >+++++++++++++++++++++++++++++++++++++
     >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
       >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        >++++++++++++++++++++++++++++++++++++++++++++
         >+++++++++++++++++++++++++++++++++++++
          >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
           >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
            >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
             >++++++++++
              >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
               >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                 >++++++++++++++++++++++++++++++++
                  >++++++++++++++++++++++++++++++++++++
                   >++++++++++++++++++++++++++++++++++++++++++++++++
                    >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                     >+++++++++++++++++++++++++++++++++++++++++++++++++
                      >++++++++++++++++++++++++++++++++++++++++++++
                       >+++++++++++++++++++++++++++++++++++++
                        >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                         >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                          >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                           >++++++++++
                            >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                             >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                              >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                               >++++++++++++++++++++++++++++++++
                                >++++++++++++++++++++++++++++++++++++
                                 >++++++++++++++++++++++++++++++++++++++++++++++++
                                  >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                   >+++++++++++++++++++++++++++++++++++++++++++++++++
                                    >++++++++++++++++++++++++++++++++++++++++++++
                                     >+++++++++++++++++++++++++++++++++++++
                                      >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                       >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                        >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                         >++++++++++
                                          >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                           >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                            >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                             >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                              >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                               >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                                >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                                 >++++++++++
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
>[.>]<[<] at switch
>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

<[->+<]> at switch

dup seq
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> at seq
   <   <   <   <   <   << at out44
[-]>[-]>[-]>[-]>[-]>[-]>>
[-<<<<<<<+>>>>>>>] first seq >
[-<<<<<<<+>>>>>>>] second seq <
<<<<<<< at out44
[->>+>>+>>>+<<<<<<<] >
[->>+>>+>>>+<<<<<<<] >>>>>>
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< at switch

--[
  +[
    [-]
    >.[-]----------------------------------------------------------------------------------------------------------
    <
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> at stack
    [>]+>+
    [<]>
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< at switch
    
  ]for end
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> at out48
  <<[-]>[-]>
  [-]>[-]
  >>>>> at stack
  [>]
  <[[<]<<<<<< + >>>>>>>[>]<-]
  <[[<]<<<<<<<+>>>>>>>>[>]<-] stack to out46
  <[<]< at seq2
  
  -----------------------------------------------------------------[
    <+>[->+<]
  ]
  >[<-------------------------->[-<+>]]<
  ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  <->

  <<<< at out48
  <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< at switch

  >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
   >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     >>>>>--------------------------
          >>------------------------------------------------------------------------------------
            >---------------------------------------------------------------------
             >-----------------------------------------------------------------------------------
              >------------------------------------------------------------------------------------
               >------------------------------------------------------------------
                >>----
                  >----------------
                   >----------------------------------------------------------------------------------------
                    >----------------------------------------------------------------------
                     >----------------------------------------------------------------------
                      >------------
                       >----------------
                        >----------------------------------------------------------------------------------------
                         >----------------
                          >--------
                           >-----
                            >----------------------------------------------------------------------------------
                             >------------------------------------------------------------------
                              >----------------------------------------------------------------------------------------
                               >---------
                                >>--------------------------------------------------------------------------
                                  >------------------------------------------------------------------------------------------
                                   >>>>
                                       >++++++++++++++++++++++++++++++++++++++++++++++++
                                        >++++++++++
  <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
]for start

>++++++++++++++++++++++++++++++++
 >++++++++++++++++++++++++++++++++
  >++++++++++++++++++++++++++++++++
   >++++++++++++++++++++++++++++++++
    >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     >[-]
      >[-]
       >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
        >++++++++++
         >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
          >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
           >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
            >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
             >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
              >++++++++++++++++++++++++++++++++
               >++++++++++++++++++++++++++++++++++++
                >++++++++++++++++++++++++++++++++++++++++++++++++
                 >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                  >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                   >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                    >++++++++++++++++++++++++++++++++++++++++++++
                     >++++++++++++++++++++++++++++++++++++++++++++++++
                      >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                       >++++++++++++++++++++++++++++++++++++++++++++++++
                        >++++++++++++++++++++++++++++++++++++++++
                         >+++++++++++++++++++++++++++++++++++++
                          >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                           >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                            >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                             >+++++++++++++++++++++++++++++++++++++++++
                              >++++++++++
                               >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                >++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                 >++++++++++++++++++++++++++++++++
                                  >+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                                   >>>++++++++++
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> at out46
 [<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>>>>>>>>>>>-]
>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>>>>>>>>>>>-] at out47
>[>>>>>>[>]+[<]<<<<<-[>>>>>>[>]<+[<]<<<<<-]] at out48
>[>>>>> [>]+[<] <<<<-[>>>>> [>]<+[<] <<<<-]]

at out49 >>
> at seq2
------------------------------------------------------------------------------------------[
  <->[>-<+]
]
>[<++++++++++++++++++++++++++>[+<->]]<
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
<+
<<
at out49 <<<
<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< at switch

>[.>]<[<] at switch
>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]>[-]<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

<<[<]> at the head of code
]

[-]>[-]>[-]<<

+++++++++++++++++++++++++++++++++++++>
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++>
++++++++++
<.
-------------.
+++++++++++++++.
---.
>.

<<+++++++++.
>----------.
+++++.
+++.
-------------.
-.
+++++++++++.
<--------------.
>-------------.
+++.
+++++++.
------.
++++++.
-------.
>.

<<++++++++++++++.
>+++.
+++++++++.
----------.
>.
